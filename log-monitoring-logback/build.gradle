import groovy.lang.Closure

import java.security.MessageDigest

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.astronuts'
version = '0.0.1'
archivesBaseName = "log-monitoring-logback"
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'ch.qos.logback:logback-classic:1.5.4'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.3.1'
    implementation 'org.slf4j:slf4j-api:2.0.12'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

// Publishing to Maven Central
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Log Monitoring Logback'
                description = 'A custom Logback appender for log monitoring and shipping.'
                url = 'https://github.com/astronuts-app/log-monitoring'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'arunkpatra'
                        name = 'Arun Patra'
                        email = 'arun@astronuts.io'
                    }
                }

                scm {
                    connection = 'scm:git:github.com/astronuts-app/log-monitoring.git'
                    developerConnection = 'scm:git:ssh://github.com/astronuts-app/log-monitoring.git'
                    url = 'https://github.com/astronuts-app/log-monitoring'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv('OSSRH_USERNAME')
                password = System.getenv('OSSRH_PASSWORD')
            }
        }
    }
}



// Only attempt to sign if we're not publishing to Maven Local
//if (gradle.taskGraph.allTasks.any { it.name == 'publishToMavenLocal' }) {
signing {
    useInMemoryPgpKeys(System.getenv("SIGNING_KEY_ID"), base64Decode(System.getenv("SIGNING_KEY")), System.getenv("SIGNING_PASSWORD"))
    sign publishing.publications
}

static def base64Decode(encodedString){
    if(encodedString != null) {
        byte[] decoded = encodedString.decodeBase64()
        String decode = new String(decoded)
        return decode
    }
    return null;
}

tasks.register('generateJarChecksums') {
    dependsOn jar

    doLast {
        // Define the files for which to generate checksums
        def filesToChecksum = [jar.archiveFile.get().asFile]

        // Generate checksums
        filesToChecksum.each { file ->
            ['MD5', 'SHA1'].each { type ->
                def checksum = generateChecksum(file, type)
                def checksumFile = new File(file.parentFile, "${file.name}.${type.toLowerCase()}")
                checksumFile.text = checksum
                println "Generated ${type} checksum for ${file.name} at ${checksumFile}"
            }
        }
    }
}

// Task for generating checksums for the POM file
tasks.register('generatePomChecksums') {
    dependsOn publishToMavenLocal

    doLast {
        def pomFile = file("${buildDir}/publications/mavenJava/pom-default.xml")
        if (pomFile.exists()) {
            ['MD5', 'SHA1'].each { type ->
                def checksum = generateChecksum(pomFile, type)
                def checksumFile = new File(pomFile.parentFile, "${pomFile.name}.${type.toLowerCase()}")
                checksumFile.text = checksum
                println "Generated ${type} checksum for ${pomFile.name} at ${checksumFile}"
            }
        }
    }
}

static String generateChecksum(File file, String algorithm) {
    MessageDigest digest = MessageDigest.getInstance(algorithm)
    file.withInputStream { fis ->
        byte[] buffer = new byte[8192]
        int read = 0
        while ((read = fis.read(buffer)) > 0) {
            digest.update(buffer, 0, read)
        }
    }
    byte[] hashBytes = digest.digest()
    return hashBytes.collect(({ String.format('%02x', it) } as Closure<Object>)).join('')
}

//publishToMavenLocal.dependsOn generateJarChecksums
//publishToMavenLocal.dependsOn generatePomChecksums

//}

// Signing the published artifacts
//signing {
//    useInMemoryPgpKeys(System.getenv("MAVEN_SIGNING_KEY_ID"), System.getenv("MAVEN_SIGNING_KEY"), System.getenv("MAVEN_SIGNING_PASSWORD"))
//    //sign publishing.publications.mavenJava
//    sign configurations.archives
//}

java {
    withSourcesJar()
    withJavadocJar()
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}